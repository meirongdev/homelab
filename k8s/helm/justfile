namespace := "monitoring"
set dotenv-load

add-repos:
    helm repo add prometheus-community https://prometheus-community.github.io/helm-charts
    helm repo add grafana https://grafana.github.io/helm-charts
    helm repo add nfs-subdir-external-provisioner https://kubernetes-sigs.github.io/nfs-subdir-external-provisioner
    helm repo add bitnami https://charts.bitnami.com/bitnami
    helm repo add hashicorp https://helm.releases.hashicorp.com
    helm repo add external-secrets https://charts.external-secrets.io
    helm repo update

create-namespace:
    kubectl create namespace {{namespace}} --dry-run=client -o yaml | kubectl apply -f -

create-grafana-secret:
    @echo "ğŸ”’ Grafana secrets are managed by Vault and External Secrets Operator."
    @kubectl get secret grafana-admin-credentials -n {{namespace}} > /dev/null 2>&1 || (echo "âŒ Error: grafana-admin-credentials secret not found. Check Vault/ESO sync." && exit 1)

create-cloudflare-secret:
    @echo "ğŸ”’ Cloudflare secrets are managed by Vault and External Secrets Operator."
    @kubectl create namespace cloudflare --dry-run=client -o yaml | kubectl apply -f -
    @kubectl get secret cloudflare-tunnel-token -n cloudflare > /dev/null 2>&1 || (echo "âŒ Error: cloudflare-tunnel-token secret not found. Check Vault/ESO sync." && exit 1)

# éƒ¨ç½² Prometheus + Grafana
deploy-prometheus: create-grafana-secret
    @echo "â³ Deploying Prometheus stack (this may take 5-10 minutes)..."
    helm upgrade --install kube-prometheus-stack prometheus-community/kube-prometheus-stack \
        --namespace {{namespace}} \
        --values values/kube-prometheus-stack.yaml \
        --wait \
        --timeout 15m \
        --debug

deploy-prometheus-nowait: create-grafana-secret
    helm upgrade --install kube-prometheus-stack prometheus-community/kube-prometheus-stack \
        --namespace {{namespace}} \
        --values values/kube-prometheus-stack.yaml \
        --timeout 15m
    @echo "âœ… Helm release created. Check status with: just status"

deploy-loki:
    helm upgrade --install loki grafana/loki \
        --namespace {{namespace}} \
        --values values/loki.yaml \
        --wait \
        --timeout 10m

deploy-tempo:
    helm upgrade --install tempo grafana/tempo \
        --namespace {{namespace}} \
        --values values/tempo.yaml \
        --wait \
        --timeout 10m

# â¬‡ï¸ ä¸ªäººæœåŠ¡ç›¸å…³å‘½ä»¤ â¬‡ï¸
personal_ns := "personal-services"

# éƒ¨ç½² Calibre-Web (ä½¿ç”¨åŸç”Ÿ K8s æ¸…å•)
deploy-calibre:
    kubectl apply -f manifests/calibre-web.yaml

# åˆ é™¤ Calibre-Web
delete-calibre:
    kubectl delete -f manifests/calibre-web.yaml

# éƒ¨ç½² IT-Tools
deploy-it-tools:
    kubectl apply -f manifests/it-tools.yaml

# åˆ é™¤ IT-Tools
delete-it-tools:
    kubectl delete -f manifests/it-tools.yaml

# éƒ¨ç½² Stirling-PDF
deploy-pdf:
    kubectl apply -f manifests/stirling-pdf.yaml

# åˆ é™¤ Stirling-PDF
delete-pdf:
    kubectl delete -f manifests/stirling-pdf.yaml

# éƒ¨ç½² Cloudflare Tunnel
deploy-cloudflare-tunnel: create-cloudflare-secret
    kubectl apply -f manifests/cloudflare-tunnel.yaml

# åˆ é™¤ Cloudflare Tunnel
delete-cloudflare-tunnel:
    kubectl delete -f manifests/cloudflare-tunnel.yaml

# éƒ¨ç½² Gateway API é…ç½® (Traefik + Gateway + HTTPRoutes)
deploy-gateway:
    kubectl apply -f manifests/traefik-config.yaml
    @echo "â³ Waiting for Traefik to restart with Gateway API provider..."
    sleep 10
    kubectl apply -f manifests/gateway.yaml
    @echo "âœ… Gateway API deployed!"

# åˆ é™¤ Gateway API é…ç½®
delete-gateway:
    kubectl delete -f manifests/gateway.yaml || true
    kubectl delete -f manifests/traefik-config.yaml || true

# æŸ¥çœ‹ä¸ªäººæœåŠ¡çŠ¶æ€
status-personal:
    kubectl get all,pvc -n {{personal_ns}}

# â¬‡ï¸ Homepage ä»ªè¡¨ç›˜ â¬‡ï¸
deploy-homepage:
    kubectl apply -f manifests/homepage.yaml
    @echo "âœ… Homepage deployed! Access at http://<NodeIP>:30080"

delete-homepage:
    kubectl delete -f manifests/homepage.yaml

# æ›´æ–° Homepage é…ç½®å¹¶é‡å¯ï¼ˆArgoCD ç®¡ç†ä¸‹ç›´æ¥ apply + restartï¼‰
update-homepage:
    kubectl apply -f manifests/homepage.yaml
    kubectl rollout restart deployment/homepage -n homepage
    kubectl rollout status deployment/homepage -n homepage --timeout=60s

homepage:
    @echo "Homepage is available at http://localhost:8080"
    kubectl port-forward -n homepage svc/homepage 8080:3000

deploy-all: add-repos create-namespace deploy-prometheus deploy-loki deploy-tempo
    @echo "âœ… All monitoring components deployed successfully!"

# æ£€æŸ¥éƒ¨ç½²çŠ¶æ€ï¼ˆè¯¦ç»†ç‰ˆï¼‰
status:
    @echo "=== Helm Releases ==="
    helm list -n {{namespace}}
    @echo ""
    @echo "=== Pods Status ==="
    kubectl get pods -n {{namespace}} -o wide
    @echo ""
    @echo "=== Events (Recent) ==="
    kubectl get events -n {{namespace}} --sort-by='.lastTimestamp' | tail -20
    @echo ""
    @echo "=== PVC Status ==="
    kubectl get pvc -n {{namespace}}

# æŸ¥çœ‹å¤±è´¥çš„ pod è¯¦æƒ…
debug-pods:
    #!/usr/bin/env bash
    echo "=== Checking for problematic pods ==="
    kubectl get pods -n {{namespace}} | grep -v "Running\|Completed" || echo "All pods are running"
    echo ""
    echo "=== Describing non-running pods ==="
    for pod in $(kubectl get pods -n {{namespace}} -o json | jq -r '.items[] | select(.status.phase!="Running" and .status.phase!="Succeeded") | .metadata.name'); do
        echo "--- Pod: $pod ---"
        kubectl describe pod $pod -n {{namespace}} | tail -30
        echo ""
    done

# æŸ¥çœ‹ç‰¹å®š pod çš„æ—¥å¿—
logs pod:
    kubectl logs {{pod}} -n {{namespace}} --tail=100 --follow

remove-all:
    helm uninstall kube-prometheus-stack -n {{namespace}} || true
    helm uninstall loki -n {{namespace}} || true
    helm uninstall tempo -n {{namespace}} || true
    helm uninstall calibre-web -n {{personal_ns}} || true
    kubectl delete secret grafana-admin-credentials -n {{namespace}} || true
    kubectl delete namespace {{namespace}} || true
    kubectl delete namespace {{personal_ns}} || true

get-grafana-password:
    @kubectl get secret -n {{namespace}} grafana-admin-credentials -o jsonpath="{.data.admin-password}" | base64 -d
    @echo ""

grafana:
    @echo "Grafana is available at http://localhost:3000"
    @echo "Username: admin"
    @echo "Password: (run 'just get-grafana-password' to retrieve)"
    kubectl port-forward -n {{namespace}} svc/kube-prometheus-stack-grafana 3000:80

prometheus:
    @echo "Prometheus is available at http://localhost:9090"
    kubectl port-forward -n {{namespace}} svc/kube-prometheus-stack-prometheus 9090:9090

init:
    #!/usr/bin/env bash
    if [ ! -f .env ]; then
        cp .env.example .env
        echo "âœ… Created .env file from .env.example"
        echo "âš ï¸  Please edit .env and set your passwords before deploying!"
    else
        echo "âœ… .env file already exists"
    fi

# æŸ¥çœ‹é›†ç¾¤èµ„æºä½¿ç”¨æƒ…å†µ
check-resources:
    @echo "=== Node Resources ==="
    kubectl top nodes
    @echo ""
    @echo "=== Pod Resources in {{namespace}} ==="
    kubectl top pods -n {{namespace}}

setup-nfs-provisioner: add-repos
    helm upgrade --install nfs-client nfs-subdir-external-provisioner/nfs-subdir-external-provisioner \
      --namespace default \
      --values values/nfs-values.yaml

# Update Prometheus stack
update-prometheus:
    helm upgrade kube-prometheus-stack prometheus-community/kube-prometheus-stack \
      -n monitoring \
      -f values/kube-prometheus-stack.yaml

# setup postgres
setup-postgres: add-repos
    helm upgrade --install postgresql bitnami/postgresql -n database \
    --create-namespace \
    -f values/postgresql-values.yaml

# get postgres auto-generated password
get-postgres-password:
    @kubectl get secret --namespace database postgresql -o jsonpath="{.data.postgres-password}" | base64 -d
    @echo ""

# â¬‡ï¸ Vault & Secret Management â¬‡ï¸
vault_ns := "vault"

deploy-vault:
    kubectl create namespace {{vault_ns}} --dry-run=client -o yaml | kubectl apply -f -
    helm upgrade --install vault hashicorp/vault \
        --namespace {{vault_ns}} \
        --values values/vault-values.yaml

# åˆå§‹åŒ– Vault (ä»…ç¬¬ä¸€æ¬¡è¿è¡Œ)
vault-init:
    kubectl exec vault-0 -n {{vault_ns}} -- vault operator init -key-shares=1 -key-threshold=1 -format=json > vault-keys.json
    @echo "âœ… Vault initialized. Keys saved to vault-keys.json (DO NOT COMMIT THIS FILE)"

# è§£é” Vault (åŸºäº vault-keys.json)
vault-unseal:
    #!/usr/bin/env bash
    set -euo pipefail
    key=$(jq -r ".unseal_keys_b64[0]" vault-keys.json)
    kubectl exec vault-0 -n {{vault_ns}} -- vault operator unseal $key
    echo "âœ… Vault unsealed."

# éƒ¨ç½² External Secrets Operator
deploy-eso:
    helm upgrade --install external-secrets external-secrets/external-secrets \
        --namespace external-secrets \
        --create-namespace \
        --values values/external-secrets-values.yaml

# é…ç½® Vault ä¸ ESO çš„è¿æ¥ (éœ€è¦å…ˆ init)
setup-vault-eso:
    #!/usr/bin/env bash
    set -euo pipefail
    token=$(jq -r ".root_token" vault-keys.json)
    kubectl create secret generic vault-token \
        --namespace external-secrets \
        --from-literal=token=$token \
        --dry-run=client -o yaml | kubectl apply -f -
    kubectl apply -f manifests/vault-eso-config.yaml

# å¤‡ä»½ vault-keys.json ä¸º K8s Secretï¼ˆé˜²æ­¢ä¸¢å¤±ï¼‰
vault-backup-keys:
    #!/usr/bin/env bash
    set -euo pipefail
    kubectl create secret generic vault-backup-keys \
        --namespace {{vault_ns}} \
        --from-literal=vault-keys.json="$(cat vault-keys.json)" \
        --dry-run=client -o yaml | kubectl apply -f -
    echo "âœ… vault-keys.json backed up to K8s secret vault/vault-backup-keys"

# â¬‡ï¸ ArgoCD GitOps â¬‡ï¸
argocd_ns := "argocd"

# ä¸€é”®å®‰è£… ArgoCD + åº”ç”¨é…ç½®ï¼ˆå¹‚ç­‰ï¼Œå¯é‡å¤æ‰§è¡Œï¼‰
deploy-argocd:
    # 1. åˆ›å»º namespaceï¼ˆidempotentï¼‰
    kubectl create namespace {{argocd_ns}} --dry-run=client -o yaml | kubectl apply -f -
    # 2. å®‰è£…/æ›´æ–° ArgoCD æ ¸å¿ƒç»„ä»¶ï¼ˆ--server-side é¿å… CRD æ³¨é‡Šè¶…é™é—®é¢˜ï¼‰
    kubectl apply -n {{argocd_ns}} -f https://raw.githubusercontent.com/argoproj/argo-cd/stable/manifests/install.yaml --server-side
    @echo "â³ Waiting for ArgoCD server to be ready..."
    kubectl rollout status deployment/argocd-server -n {{argocd_ns}} --timeout=8m
    # 3. ç¦ç”¨å†…ç½® TLSï¼ˆç”± Traefik å¤„ç†ï¼‰
    kubectl apply -f ../../argocd/install/argocd-cm-patch.yaml
    kubectl rollout restart deployment/argocd-server -n {{argocd_ns}}
    kubectl rollout status deployment/argocd-server -n {{argocd_ns}} --timeout=3m
    # 4. æ³¨å†Œ Gateway HTTPRouteï¼ˆargocd namespace å·²å­˜åœ¨ï¼‰
    kubectl apply -f manifests/gateway.yaml
    # 5. æ³¨å†Œ AppProject + Applications
    kubectl apply -f ../../argocd/projects/homelab.yaml
    kubectl apply -f ../../argocd/applications/
    @echo "âœ… ArgoCD deployed! Access at https://argocd.meirong.dev"
    @echo "Initial password: run 'just argocd-password'"

# æ›´æ–° Cloudflare DNSï¼ˆæ·»åŠ  argocd.meirong.devï¼‰
deploy-argocd-dns:
    cd ../../cloudflare/terraform && just apply

# è·å– ArgoCD åˆå§‹ç®¡ç†å‘˜å¯†ç 
argocd-password:
    @kubectl -n {{argocd_ns}} get secret argocd-initial-admin-secret -o jsonpath="{.data.password}" | base64 -d
    @echo ""

# è§¦å‘å…¨é‡åŒæ­¥ï¼ˆé€šè¿‡ kubectl annotationï¼Œæ— éœ€ argocd CLIï¼‰
argocd-sync:
    @echo "â³ Triggering sync for all applications..."
    @kubectl get applications -n {{argocd_ns}} -o name | xargs -I{} kubectl annotate {} -n {{argocd_ns}} argocd.argoproj.io/refresh=normal --overwrite
    @echo "âœ… Sync triggered. Waiting for completion..."
    @sleep 15
    @kubectl get applications -n {{argocd_ns}}

# æŸ¥çœ‹æ‰€æœ‰ Application çŠ¶æ€
argocd-status:
    @echo "=== ArgoCD Applications ==="
    kubectl get applications -n {{argocd_ns}}
    @echo ""
    @echo "=== ArgoCD Pods ==="
    kubectl get pods -n {{argocd_ns}}

# â¬‡ï¸ Kopia Backup Server â¬‡ï¸
kopia_ns := "kopia"

# æ·»åŠ  Kopia æœåŠ¡å™¨ç”¨æˆ·ï¼ˆæ ¼å¼: username@hostnameï¼‰
kopia-add-user user:
    #!/usr/bin/env bash
    set -euo pipefail
    pod=$(kubectl get pod -n {{kopia_ns}} -l app=kopia -o jsonpath='{.items[0].metadata.name}')
    password=$(kubectl get secret -n {{kopia_ns}} kopia-secret -o jsonpath='{.data.password}' | base64 -d)
    kubectl exec -n {{kopia_ns}} "$pod" -c kopia -- kopia server user add "{{user}}" --user-password="$password"
    kubectl rollout restart deployment/kopia -n {{kopia_ns}}
    kubectl rollout status deployment/kopia -n {{kopia_ns}} --timeout=90s
    echo "âœ… User '{{user}}' added. Connect with:"
    echo "   kopia repository connect server \\"
    echo "     --url=https://10.10.10.10:31515 \\"
    echo "     --server-cert-fingerprint=$(just kopia-fingerprint) \\"
    echo "     --override-username=$(echo '{{user}}' | cut -d@ -f1)"

# åˆ—å‡º Kopia æœåŠ¡å™¨ç”¨æˆ·
kopia-list-users:
    #!/usr/bin/env bash
    pod=$(kubectl get pod -n {{kopia_ns}} -l app=kopia -o jsonpath='{.items[0].metadata.name}')
    kubectl exec -n {{kopia_ns}} "$pod" -c kopia -- kopia server user list

# è·å– Kopia TLS è¯ä¹¦æŒ‡çº¹
kopia-fingerprint:
    @openssl s_client -connect 10.10.10.10:31515 </dev/null 2>/dev/null | openssl x509 -fingerprint -sha256 -noout | sed 's/://g' | cut -d= -f2 | tr '[:upper:]' '[:lower:]'

# æŸ¥çœ‹ Kopia æœåŠ¡å™¨çŠ¶æ€
kopia-status:
    @echo "=== Kopia Pods ==="
    @kubectl get pods -n {{kopia_ns}}
    @echo ""
    @echo "=== Kopia PVCs ==="
    @kubectl get pvc -n {{kopia_ns}}
    @echo ""
    @echo "=== Registered Users ==="
    @just kopia-list-users

# Vault UI info
vault-ui:
    @echo "Vault UI is available at http://localhost:30820 (NodePort)"
    @echo "Root Token for login:"
    @jq -r ".root_token" vault-keys.json
    @echo "Starting port-forward to http://localhost:8200 ... Press Ctrl+C to stop."
    kubectl port-forward -n {{vault_ns}} svc/vault 8200:8200
